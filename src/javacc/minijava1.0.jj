PARSER_BEGIN(JVMMain)

package mjc;

import java.io.*;
import syntaxtree.*;
import symboltree.*;
import visitor.*;
import error.*;

import java.math.BigInteger;
import java.util.ArrayList;
import java.io.PrintStream;
import java.io.File;

/**
 * Grammar to parse MiniJava language 
 * @author Christopher Teljstedt and Carl Eriksson 
 */
public class JVMMain
{

/*
 * Set this to true for more debug outprints.
 */
public final static boolean VERBOSE = false;

public JVMMain (String fileName){
	this(System.in);
    try { ReInit(new FileInputStream(new File(fileName))); }
    catch(Exception e) { e.printStackTrace(); }
}

/*
 * The compiler starting point
 *
 * Input arguments shall contain path to the source file to compile.
 */
public static void main(String args[]) {
	JVMMain parser = null;
  Program program;
  SyntaxTreePrinter stp;
  SymbolTableBuilderVisitor stb;
  TypeDepthFirstVisitor tdfv;
  BytecodeEmitterVisitor bev;

  //Reading from standard input
	if (args.length == 0) {
		System.out.println("MiniJavac 1.0:  Reading from standard input . . .");
		parser = new JVMMain(System.in);
	} 
  //Reading for file
  else if (args.length == 1) {
		System.out.println("MiniJavac 1.0:  Reading from file " + args[0] + " . . .");
		try {
			parser = new JVMMain(new java.io.FileInputStream(args[0]));
		} catch (java.io.FileNotFoundException e) {
			System.out.println("MiniJavac 1.0: File " + args[0] + " not found.");
			System.exit(1);
		}
  } 
  //Reading from file and take target platform
  else if (args.length == 2){
    System.out.println("MiniJavac 1.0:  Reading from file " + args[0] + " . . .");
    try {
      parser = new JVMMain(new java.io.FileInputStream(args[0]));
    } catch (java.io.FileNotFoundException e) {
      System.out.println("MiniJavac 1.0: File " + args[0] + " not found.");
      System.exit(1);
    }  
	}
  //Help outprints
  else {
		System.out.println("MiniJavac 1.0:  Usage is one of:");
		System.out.println("         java JavaParser < inputfile");
		System.out.println("OR");
		System.out.println("         java JavaParser inputfile");
		System.exit(0);
	}
  /*
   * Now start to to compile the file.
   */
	try {
		program = parser.Program();

    
    // VERBOSE: Print abstract syntax tree 
    if(VERBOSE) {
      stp = new SyntaxTreePrinter(System.out);
      stp.visit(program);
      System.out.println();
    }
   
    //Symbol table
    stb = new SymbolTableBuilderVisitor();
    stb.visit(program);

    // VERBOSE: Output symbol table
    if(VERBOSE) {
      Table st = stb.scopeLookupTable.get(program);
      System.out.println(st.toString(0));
    }
    
    //Type checking.
    tdfv = new TypeDepthFirstVisitor();
    tdfv.scopeLookupTable = stb.scopeLookupTable;
    tdfv.visit(program);

    //Jasmine code
    bev = new BytecodeEmitterVisitor();
    bev.scopeLookupTable = tdfv.scopeLookupTable;
    bev.typeChecker = tdfv;
    bev.visit(program);

    //Print any errors from the symbol table
    if(stb.hasErrors()){
      for(CompilerError err : stb.getErrors()){
          System.err.println(err);
      }
      throw new Exception("Symbol table error");
    }

    //Print any errors form the type checking
    if(tdfv.hasErrors()){
       for(CompilerError err : tdfv.getErrors()){
          System.err.println(err);
       }
       throw new Exception("Type checker error");
    }

		System.out.println("MiniJavac 1.0: Java program parsed successfully.");
	} catch (Exception e) {
		System.out.println(e.getMessage());
		System.out.println("MiniJavac 1.0: Encountered errors during parse.");
    System.exit(1);
	}
  System.exit(0);
}
}
PARSER_END(JVMMain)

TOKEN:  /* RESERVED WORDS AND LITERALS */
{
  < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < ELSE: "else" >
| < FALSE: "false" >
| < IF: "if" >
| < INT: "int" >
| < LONG: "long" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < THIS: "this" >
| < TRUE: "true" >
| < VOID: "void" >
| < WHILE: "while" >
| < LENGTH: "length" >
| < EXTENDS: "extends" >
| < STRING: "String" >
| < SYSOUT_PRINTLN : "System.out.println" >
}

TOKEN: /* SEPARATORS */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < NOT: "!" >
}


TOKEN: /* LITERALS */
{
  < INTEGER_LITERAL: ( ["0"] | ["1"-"9"](["0"-"9"])*) >
|
  < LONG_LITERAL: ("0" | ["1"-"9"](["0"-"9"])*) ["l", "L"] >
}


TOKEN: /* IDENTIFIER LITTERAL */
{
  < IDENTIFIER: (["a"-"z", "A"-"Z", "_"]) (["a"-"z", "A"-"Z", "0"-"9", "_"])* >
}

TOKEN: /* OPERATORS */
{
  < ASSIGN: "=" >
| < LT: "<" >
| < GT: ">" >
| < EQ: "==" > /* Bonus 1p */
| < LE: "<=" > /* Bonus 1p */
| < GE: ">=" > /* Bonus 1p */
| < NE: "!=" > /* Bonus 1p */
| < SC_OR: "||" > /* Bonus 2p */
| < SC_AND: "&&" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
}

/* COMMENTS */
SKIP : { " " | "\u207b" | "\t" | "\n" | "\f" | "//":IN_SINGLE_LINE_COMMENT }

MORE :
{ "/*" : IN_MULTI_LINE_COMMENT }

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: (~["\n","\r"])* ("\n" | "\r" | "\r\n")? > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN : { <MULTI_LINE_COMMENT: "*/" > : DEFAULT }

<IN_SINGLE_LINE_COMMENT,IN_MULTI_LINE_COMMENT>
MORE : {  < ~[] > }

/**********************************************
 * THE MINI JAVA LANGUAGE GRAMMAR STARTS HERE *
 **********************************************/
/*
 * Program structuring syntax follows.
 */

/* Top level production */
Program Program():
{ MainClass mc; ClassDeclList cdl; }
{
  /* The first class must be the main class */
  mc=MainClass()

  /* Then zero or more other class declarations */
  cdl = ClassDeclList() 

  /* Reached end of file */
  < EOF >
  { return new Program(mc, cdl); }
}

/* Class declaration 
 * 
 * Function:    ClassDeclList() 
 * Description: Matches zero or more classes,
 *              with our without "extends" 
 */
ClassDeclList ClassDeclList():
{ ClassDecl c; ClassDeclList cdl = new ClassDeclList(); }
{
  (
      LOOKAHEAD(3)
      c = ClassDecl()
      { cdl.addElement(c); } 
    |
      c = ClassDeclExtends()
      { cdl.addElement(c); }
  )*
  { return cdl; }
}

/* Main declaration 
 * 
 * Function:    MainClass() 
 * Description: Matches main standard template.
 *              
 * Important:   Since the "main" is not a reserved word it 
 *              cannot be a token. Identifiers are allowed
 *              to be called "main".
 */
MainClass MainClass():
{ Token t; Token main; Identifier id1, id2; VarDecl v; Statement stmt; VarDeclList vdl = new VarDeclList(); StatementList sl = new StatementList(); }
{
  <CLASS>
  t = <IDENTIFIER> { id1 = new Identifier(t.image, t.endLine, t.endColumn); }
  <LBRACE>
      <PUBLIC> <STATIC> <VOID> main = <IDENTIFIER> <LPAREN> <STRING> <LBRACKET> <RBRACKET> t = <IDENTIFIER> { id2 = new Identifier(t.image, t.endLine, t.endColumn); } <RPAREN>
      <LBRACE> //Block
      ( LOOKAHEAD( Type() <IDENTIFIER> ) v = VarDecl() { vdl.addElement(v); } )* 
      ( stmt = Stmt() { sl.addElement(stmt); } )* 
      <RBRACE>
  <RBRACE>
  { 
    if(!main.image.equals("main")){
      System.exit(1);
    };
  }
  { return new MainClass(id1, id2, vdl, sl, main.endLine, main.endColumn); }
}

/* Class extends declaration 
 * 
 * Function:    ClassDeclExtends() 
 * Description: Matches an extended class.
 *        
 */
ClassDeclExtends ClassDeclExtends():
{
  Token t; Identifier id, ide; VarDecl v; MethodDecl m; VarDeclList vl = new VarDeclList(); MethodDeclList ml = new MethodDeclList();
}
{
  <CLASS> t=<IDENTIFIER>  { id=new Identifier(t.image, t.endLine, t.endColumn); } <EXTENDS> t = <IDENTIFIER> { ide = new Identifier(t.image, t.endLine, t.endColumn); } 
  <LBRACE> //Block
       ( LOOKAHEAD(2) v = VarDecl() {vl.addElement(v); } )* ( m = MethodDecl() { ml.addElement(m); } )* 
  <RBRACE>
  { return new ClassDeclExtends(id, ide, vl, ml, t.endLine, t.endColumn); }
}

/* Class extends declaration 
 * 
 * Function:    ClassDecl() 
 * Description: Matches an class without "extends".
 */
ClassDeclSimple ClassDecl():
{
  Token t; Identifier id, ide; VarDecl v; MethodDecl m; VarDeclList vl = new VarDeclList(); MethodDeclList ml = new MethodDeclList();
}
{
  <CLASS> t=<IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); }
  <LBRACE> //Block
       ( LOOKAHEAD(2) v = VarDecl() {vl.addElement(v); } )* ( m = MethodDecl() { ml.addElement(m); } )* 
  <RBRACE>
  { return new ClassDeclSimple(id, vl, ml, t.endLine, t.endColumn); }
}

/* Variable declaration 
 * 
 * Function:    VarDecl() 
 * Description: Matches an identifier.
 */
VarDecl VarDecl() :
{ Token t; Type type; Identifier id; }
{
  ( type=Type() t = <IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); } <SEMICOLON> )  { return new VarDecl(type, id); }
}

/* Method declaration 
 * 
 * Function:    MethodDecl() 
 * Description: Matches a method, which can contain zero or more
 *              local variable declarations, followed by zero or 
 *              more statements, followed by a return expression.
 */
MethodDecl MethodDecl():
{ Exp e; Token t; Type type; Identifier id;  VarDecl v; Statement s; FormalList fl = new FormalList(); VarDeclList vl = new VarDeclList(); StatementList sl = new StatementList(); }
{
  <PUBLIC> type = Type() ( t = <IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); } ) <LPAREN> [ fl = FormalList() ] <RPAREN>
  <LBRACE> 
    ( LOOKAHEAD(2) v = VarDecl() { vl.addElement(v); } )* 
    ( s = Stmt() { sl.addElement(s); } )*  
    <RETURN> e = Exp() <SEMICOLON>
  <RBRACE>
  { return new MethodDecl(type, id, fl, vl, sl, e, t.endLine, t.endColumn); }
}

/* Formal list 
 * 
 * Function:    FormalList() 
 * Description: Matches formal parameters in a function.
 *              Can match zero or more parameters, uses
 *              a helper function FormalRest() to handle
 *              comma separated parameters.
 *
 */
FormalList FormalList():
{ Token t; Type type; Identifier id; Formal f; FormalList fl = new FormalList(); }
{
  type=Type() t=<IDENTIFIER> { id=new Identifier(t.image, t.endLine, t.endColumn); } { f = new Formal(type, id, t.endLine, t.endColumn); fl.addElement(f); } ( f=FormalRest() { fl.addElement(f); } )* 
  { return fl; }
}

/* Formal list helper function
 * 
 * Function:    FormalRest() 
 * Description: Helper function, see FormalList()
 *
 */
Formal FormalRest():
{ Token t; Type type; Identifier id; }
{
  <COMMA> type=Type() ( t=<IDENTIFIER> { id=new Identifier(t.image, t.endLine, t.endColumn); } ) { return new Formal(type, id); }
}

/* Types
 * 
 * Function:    Type() 
 * Description: Matches primitive types and objects
 *
 */
Type Type() :
{ Token t; Type type; }
{
  (
      (
          LOOKAHEAD(2)
          <INT> <LBRACKET> <RBRACKET> { type=new IntArrayType(); }
        | <INT> { type=new IntegerType(); }
      )
    |
      (
          LOOKAHEAD(2)
          <LONG> <LBRACKET> <RBRACKET> { type=new LongArrayType(); }
        | <LONG> { type=new LongType(); }
      )
    | <BOOLEAN> { type=new BooleanType(); }  
    | t=<IDENTIFIER> { type=new IdentifierType(t.image, t.endLine, t.endColumn); }
  )
  { return type; }
}

/* Statements 
 * 
 * Function:    Stmt() 
 * Description: Matches statements such as "if", "while"
 *              curly bracers, etd.
 *
 */
Statement Stmt():
{ Token t; Exp e1, e2; Identifier id; Statement rs, s1, s2; VarDecl v; StatementList sl = new StatementList(); VarDeclList vl = new VarDeclList(); }
{
  (
      (
        <LBRACE> 
          ( LOOKAHEAD(2) v=VarDecl() { vl.addElement(v); } )* 
          ( s1=Stmt() { sl.addElement(s1); } )* 
        <RBRACE> 
      )
      { rs = new Block(vl,sl); }
    | /* Extension */
      t=<IF> <LPAREN> e1=Exp() <RPAREN> s1=Stmt() { rs = new If(e1, s1, t.endLine, t.endColumn); }
      [ LOOKAHEAD(1) <ELSE> s2=Stmt() { rs = new IfElse(e1, s1, s2, t.endLine, t.endColumn); } ]
      // <IF> <LPAREN> e1=Exp() <RPAREN> s1=Stmt() <ELSE> s2=Stmt() { rs = new IfElse(e1, s1, s2); } 
    | t=<WHILE> <LPAREN> e1=Exp() <RPAREN> s1=Stmt() { rs = new While(e1, s1, t.endLine, t.endColumn); }
    | <SYSOUT_PRINTLN> <LPAREN> e1=Exp() <RPAREN> <SEMICOLON>  { rs = new Print(e1); }
    | LOOKAHEAD(2)
      ( t = <IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); } <ASSIGN> e1=Exp() <SEMICOLON> )  { rs = new Assign(id, e1, t.endLine, t.endColumn); } 
    | ( t = <IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); } <LBRACKET> e1=Exp() <RBRACKET> <ASSIGN> e2=Exp() <SEMICOLON> ) { rs = new ArrayAssign(id,e1,e2, t.endLine, t.endColumn); } 
  )
  { return rs; }
}

/*
 * Exp syntax follows.
 */


/** Expressions
 *
 *  Function:     Exp()
 *  Description:  Begins the matching of expressions. Recursively stepping
 *                through each expression and doing an exhaustive matching
 *                on each precedence level. Begins with the least binding
 *                (weakest) expression "||". 
 *
 */
Exp Exp() : 
{ Token t; Exp e1, e2; }
{
  e1 = AndExp() [ LOOKAHEAD(2) e1 = ExpPrim(e1) ]
  { return e1; }
}


/** Expressions prim 
 *
 * Function:      AndExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is EqExp().
 *
 */
Exp ExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  ( 
    t=<SC_OR> e2 = AndExp() { re = new Or(e1, e2, t.endLine, t.endColumn); } 
  )
  [ LOOKAHEAD(2) re = ExpPrim(re) ]
  { return re; }
}


/** And expression
 *
 *  Function:     AndExp()
 *  Description:  Since left-recursion cannot be applied the right associative 
 *                tree is eliminated by passing the matched expression as a 
 *                paramter to AndExpPrim() so that it is built up in left 
 *                associative manner. When the matching has been exhasted
 *                it continues to next precedence level which is EqExp().
 *
 */
Exp AndExp() :
{ Token t; Exp e1, e2, re; }
{
  e1 = EqExp() [ LOOKAHEAD(2) e1 = AndExpPrim(e1) ]
  { return e1; }
}

/** And expression prim 
 *
 * Function:      AndExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is EqExp().
 *
 */
Exp AndExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  ( 
       t=<SC_AND> e2 = EqExp() { re = new And(e1, e2, t.endLine, t.endColumn); } 
  )
  [ LOOKAHEAD(2) re = AndExpPrim(re) ]
  { return re; }
}



/** Equality expression
 *
 *  Function:     EqExp()
 *  Description:  Since left-recursion cannot be applied the right associative 
 *                tree is eliminated by passing the matched expression as a 
 *                parameter to EqExpPrim() so that it is built up in left 
 *                associative manner. When the matching has been exhasted
 *                it continues to next precedence level which is RelExp().
 *
 */
Exp EqExp() :
{ Token t; Exp e1, e2, re; }
{
  e1 = RelExp() [ LOOKAHEAD(2) e1 = EqExpPrim(e1) ]
  { return e1; }
}

/** Equality expression prim 
 *
 * Function:      EqExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is RelExp().
 *
 */
Exp EqExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  ( 
      LOOKAHEAD(2)
       t=<EQ> e2 = RelExp() { re = new Equal(e1, e2, t.endLine, t.endColumn); } 
      |
       t=<NE> e2 = RelExp() { re = new NotEqual(e1, e2, t.endLine, t.endColumn); }
  )
  [ LOOKAHEAD(2) re = EqExpPrim(re) ]
  { return re; }
}

/** Relativity expressions
 *
 *  Function:     RelExp()
 *  Description:  Since left-recursion cannot be applied the right associative 
 *                tree is eliminated by passing the matched expression as a 
 *                parameter to RelExpPrim() so that it is built up in left 
 *                associative manner. When the matching has been exhasted
 *                it continues to next precedence level which is AddExp().
 *
 */
Exp RelExp() :
{ Token t; Exp e1, e2, re; }
{
  e1 = AddExp() [ LOOKAHEAD(2) e1 = RelExpPrim(e1) ]
  { return e1; }
}

/** Relativity expressions prim 
 *
 * Function:      RelExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is AddExp().
 *
 */
Exp RelExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  (   LOOKAHEAD(2) 
      t=<LT> e2 = AddExp() { re = new LessThan(e1, e2, t.endLine, t.endColumn); } 
    |
      t=<LE> e2 = AddExp() { re = new LessThanOrEqual(e1, e2, t.endLine, t.endColumn); } 
    |    
      t=<GT> e2 = AddExp() { re = new GreaterThan(e1, e2, t.endLine, t.endColumn); } 
    |
      t=<GE> e2 = AddExp() { re = new GreaterThanOrEqual(e1, e2, t.endLine, t.endColumn); } 
  )
  [ LOOKAHEAD(2) re = EqExpPrim(re) ]
  { return re; }

}

/** Additive expressions
 * 
 *  Function:     EqExp()
 *  Description:  Since left-recursion cannot be applied the right associative 
 *                tree is eliminated by passing the matched expression as a 
 *                paramter to EqExpPrim() so that it is built up in left 
 *                associative manner. When the matching has been exhasted
 *                it continues to next precedence level which is MultExp().
 *
 */
Exp AddExp() :
{ Token t; Exp e1, e2, re; }
{
  e1 = MultExp() [ LOOKAHEAD(2) e1 = AddExpPrim(e1) ]
  { return e1; }
}

/** Additive expression prim 
 *
 * Function:      EqExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is MultExp().
 *
 */
Exp AddExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  ( 
      LOOKAHEAD(2)
      t=<MINUS> e2 = MultExp() { re = new Minus(e1, e2, t.endLine, t.endColumn); } 
      |
      t=<PLUS> e2 = MultExp() { re = new Plus(e1, e2, t.endLine, t.endColumn); } 
  )
  [ LOOKAHEAD(2) re = AddExpPrim(re) ]
  { return re; }
}

/** Additive expressions
 * 
 *  Function:     MultExp()
 *  Description:  Since left-recursion cannot be applied the right associative 
 *                tree is eliminated by passing the matched expression as a 
 *                parameter to MultExpPrim() so that it is built up in left 
 *                associative manner. When the matching has been exhasted
 *                it continues to next precedence level which is PrimaryExp().
 *
 */
Exp MultExp() :
{ Token t; Exp e1, e2, re; }
{
  e1 = PrimaryExp() [ LOOKAHEAD(2) e1 = MultExpPrim(e1) ]
  { return e1; }
}


/** Multiplicative expression prim 
 *
 * Function:      MultExpPrim()
 * Description:   This function is called recursively, passing the matched
 *                expression in to next level of the recursion, building a
 *                left associative tree. When the matching has been exhasted
 *                it continues to next precedence level which is PrimaryExp().
 *
 */
Exp MultExpPrim(Exp e1) :
{ Token t; Exp e2, re; }
{
  { re = e1; }
  ( 
      t=<STAR> e2 = PrimaryExp() { re = new Times(e1, e2, t.endLine, t.endColumn); } 
  )
  [ LOOKAHEAD(2) re = MultExpPrim(re) ]
  { return re; }
}


/** Primary expressions
 * 
 *  Function:     PrimaryExp()
 *  Description:  Here resides expressions with highest precedence level
 *                Avoids left-recursion by moving all non-terminal productions
 *                to the PrimaryExpPrim.
 *
 */

Exp PrimaryExp():
{ Token t; Exp e;}
{
  (   
      (t = <INTEGER_LITERAL> { e = new IntegerLiteral(Integer.parseInt(t.image), t.endLine, t.endColumn); } ) 
    | (t = <LONG_LITERAL> { e = new LongLiteral(Long.parseLong(t.image.substring(0, t.image.length()-1)), t.endLine, t.endColumn); } ) 
    | <TRUE>  { e = new True(); }
    | <FALSE> { e = new False(); }
    | (t = <IDENTIFIER> { e = new IdentifierExp(t.image, t.endLine, t.endColumn); } )
    | <THIS> { e = new This(); } 
    | LOOKAHEAD(2)
      ( //Safeguard against [][]
            LOOKAHEAD(<NEW> <INT> <LBRACKET> Exp() <RBRACKET> <LBRACKET>)
            t=<NEW> <INT> <LBRACKET> ( e=Exp() { e = new NewIntArray(e, t.endLine, t.endColumn); } ) <RBRACKET> { return e; }
          | t=<NEW> <INT> <LBRACKET> ( e=Exp() { e = new NewIntArray(e, t.endLine, t.endColumn); } ) <RBRACKET> 
      )
    | LOOKAHEAD(2)
      ( //Safeguard against [][]
           LOOKAHEAD(<NEW> <LONG> <LBRACKET> Exp() <RBRACKET> <LBRACKET>)
            t=<NEW> <LONG> <LBRACKET> ( e=Exp() { e = new NewLongArray(e, t.endLine, t.endColumn); } ) <RBRACKET> { return e; }
          | t=<NEW> <LONG> <LBRACKET> ( e=Exp() { e = new NewLongArray(e, t.endLine, t.endColumn); } ) <RBRACKET> 

      )
    | {Token obj;} obj=<NEW> ( t = <IDENTIFIER> { e = new NewObject(new Identifier(t.image, t.endLine, t.endColumn), obj.endLine, obj.endColumn); } ) <LPAREN> <RPAREN> 
    | t=<NOT> ( e = PrimaryExp() { e = new Not(e, t.endLine, t.endColumn); } ) 
    | <LPAREN> e = Exp() <RPAREN> 
  )
  ( LOOKAHEAD(2) e=PrimaryExpPrim(e) )* //Can match epsilon
  { return e; }
}

/** Primary expressions
 * 
 *  Function:     PrimaryExpPrim()
 *  Description:  Contains all non-terminal productions from PrimaryExp()
 *
 */
Exp PrimaryExpPrim(Exp e1):
{ Token t; Exp e2; Identifier id; ExpList el; }
{
  t=<LBRACKET> e2=Exp() <RBRACKET> { return new ArrayLookup(e1, e2, t.endLine, t.endColumn); }
  | LOOKAHEAD(2)
    t=<DOT> <LENGTH> { return new ArrayLength(e1, t.endLine, t.endColumn); }
  | {Token call;} call=<DOT> (t = <IDENTIFIER> { id = new Identifier(t.image, t.endLine, t.endColumn); } ) <LPAREN> el=ExpList() <RPAREN> { return new Call(e1, id, el, call.endLine, call.endColumn); }
}

/** Expression list
 * 
 *  Function:     ExpList()
 *  Description:  Matches zero or expressions. If multiple
 *                expressions, then match COMMA as separating
 *                character.
 */
ExpList ExpList():
{ Exp e; ExpList el = new ExpList(); }
{
	[ e=Exp() { el.addElement(e); } ( e=ExpRest() { el.addElement(e); } )* ]
  { return el; }
}

/** Expression list
 * 
 *  Function:     ExpListRest()
 *  Description:  Helper function, see ExpList().
 */
Exp ExpRest():
{ Exp e; }
{
	<COMMA> e=Exp()
  { return e; }
}

